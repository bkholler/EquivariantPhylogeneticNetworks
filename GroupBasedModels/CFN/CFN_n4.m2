load "CFN_Networks.m2"


-- set number of leaves and model
n = 4
N = digraph(toList(1..8), {{5,1}, {6,2}, {7,3}, {8,4}, {6,5}, {8,5}, {7,8}, {7,6}});
M = CFNmodel


-- setup the ring and compute the vanishing ideal of the CFN model on the standard 4-sunlet
-- by results of https://arxiv.org/pdf/2312.07450, the ideal is generated by quadratics 
-- so it suffices to compute all quadratic generators of the kernel of the corresponding ring map
S = qRing(n, M);
images = sunletParam(n, M);
R = ring images_0;
phi = map(R, S, images);
H = componentsOfKernel(2, phi);
I = ideal delete(null, flatten values H)

T = leafTree({1,2,3,4}, {{1, 3}})
J = sub(phyloToric42(T, CFNmodel), ring I)

isSubset(I, J)
isSubset(J, I)


-- make a hash table where the keys are the four sunlets and the values are their ideal, created by permuting the indices of the original ideal
fourSunletIdeals = hashTable for i in generate4CyclesWithPerms() list(

    (sigma, N) := i;
    tau := (sigma / (i -> i + 1)) // permutation // inverse // toList / (i -> i -1);
    permuteVars :=  apply(gens S, j -> j => q_(toSequence (last baseName j)_tau));
    J := sub(I, permuteVars);
    {N, J}
);


-- To verify that all 4-sunlets are not identifiable from each other, we just need to show that there exists a pair of 4-sunlets for which the ideals are equal
any(subsets(keys fourSunletIdeals, 2), netPair ->  fourSunletIdeals#(netPair_0) != fourSunletIdeals#(netPair_1))

